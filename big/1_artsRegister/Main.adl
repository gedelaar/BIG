CONTEXT Arts IN DUTCH HTML
    PATTERN Arts
        PURPOSE PATTERN Arts
        {+
        Het register voor arts bevat alle attributen van het register arts.
        +}
        -- dit mag niet gewijzigd worden
        RELATION arts[RegisterId*RegisterId]
        POPULATION arts[RegisterId*RegisterId]  CONTAINS [("1","1")] 
        -- 

        RELATION getuigschriftdatum[InschrijfId*Datum]
            MEANING "Er is een getuigschrift bij een inschrijving geregistreerd dat relevant is voor het bepalen van de geldigheid van die inschrijving."
            PURPOSE RELATION getuigschriftdatum[InschrijfId*Datum]
                {+ 
                Een register registreert datum waarop de ingeschrevene een bij of krachtens hoofdstuk III of VI aangewezen getuigschrift heeft verkregen, zoals bedoeld in Art. 8 lid 2 sub a van de Wet BIG. Als er meerdere getuigschriften zijn, kunnen er dus ook meerdere data zijn bij dezelfde inschrijving.
                +}

        RELATION herregistratie[InschrijfId*Datum]
            MEANING "Artikel 2, tweede lid van het Besluit periodieke registratie Wet Big stelt dat de datum van herregistratie op vijf jaar na datum van registratie."
        
        ROLE USER MAINTAINS DefaultDeelToegang
        RULE DefaultDeelToegang : I[InschrijfId]  |-  deelToegang [InschrijfId*Ja_of_Nee];deelToegang [InschrijfId*Ja_of_Nee]~
            MEANING "meaning"
            MESSAGE "Het ja of nee moet ingevuld worden."
            VIOLATION ( TXT "Voor register ", SRC I, TXT " is geen ja of nee ingevuld.")


    ENDPATTERN
    INTERFACE Artsen FOR  MEDEWERKER       : V[SESSION*RegisterId];(I /\ arts[RegisterId*RegisterId] )  cRud
        BOX <TABLE  hideOnNoRecords title="Artsen"  sortable >
            [
               "Inschrijvingen" : inschrijving[RegisterId*InschrijfId]  LINKTO INTERFACE Inschrijving
            ,   "Persoon"       : inschrijving[RegisterId*InschrijfId];inschrijving [Persoon*InschrijfId]~ LINKTO INTERFACE Persoon
            ,   "Naam"          : (inschrijving[RegisterId*InschrijfId];inschrijving [Persoon*InschrijfId]~) ; naam[Persoon*Naam]
            ,   "BIG-nummer"    : (inschrijving[RegisterId*InschrijfId];bignummer[InschrijfId*Bignummer]) cRud
            ,   "Registratie"   :  inschrijving[RegisterId*InschrijfId];registratie [Registratie*InschrijfId]~ LINKTO INTERFACE RegistratieB

            ]

    INTERFACE RegistratieB FOR MEDEWERKER : I[Registratie] cRud
        BOX <FORM  hideOnNoRecords  title="Beroepregister"  showNavMenu>
            [   "Register"      : registratie [Registratie*InschrijfId];inschrijving[RegisterId*InschrijfId]~ cRud
            ,   "Registratie"   : I[Registratie] cRud
            ,   "Inschrijving"  : registratie [Registratie*InschrijfId] LINKTO INTERFACE Inschrijving
            ,   "Deeltoegang"   : registratie [Registratie*InschrijfId];deelToegang[InschrijfId*Ja_of_Nee] cRUd
            ,   "Persoon"       : registratie [Registratie*InschrijfId];inschrijving [Persoon*InschrijfId]~ LINKTO INTERFACE Persoon  
            ,   "BIG-nummer"    : registratie [Registratie*InschrijfId];bignummer[InschrijfId*Bignummer] cRud    
            ,   "Aantekening"   : aantekening [Registratie*Aantekening] CRUD

            ]
ENDCONTEXT
