CONTEXT Tandarts IN DUTCH HTML
INCLUDE "../0_generiek/Main.adl"

    PATTERN Tandarts
        PURPOSE PATTERN Tandarts
        {+
        Het register voor tandarts bevat alle attributen van het register tandarts.
        +}


        RELATION tandarts [RegisterId*RegisterId]
        POPULATION tandarts[RegisterId*RegisterId]  CONTAINS
            [("2","2")]

        RELATION specialisme [RegisterId*SpecialismeTandarts]
        MEANING "Alle specialismes voor tandarts." 
        POPULATION specialisme [RegisterId*SpecialismeTandarts] CONTAINS
        [
            ("2","MKA-chirurg"),
            ("2","Orthodontist")
        
        ]
        RELATION specialist [Registratie*SpecialismeTandarts] 
        MEANING "De specialist is een uitbreiding van tandarts met een specialisme."


        RELATION herregistratie[RegisterId*Datum]
            MEANING "Artikel 2, tweede lid van het Besluit periodieke registratie Wet Big stelt dat de datum van herregistratie op vijf jaar na datum van registratie."
        

    ENDPATTERN
    INTERFACE Tandarts FOR  MEDEWERKER       : V[SESSION*RegisterId];(I /\ tandarts[RegisterId*RegisterId] )  cRud
    BOX <TABLE  hideOnNoRecords sortable  order="asc" showNavMenu>
            [
               "Inschrijvingen" : inschrijving[RegisterId*InschrijfId]  LINKTO INTERFACE Inschrijving
            ,   "Persoon"       : inschrijving[RegisterId*InschrijfId];inschrijving [Persoon*InschrijfId]~ LINKTO INTERFACE Persoon
            ,   "Naam"          : (inschrijving[RegisterId*InschrijfId];inschrijving [Persoon*InschrijfId]~) ; naam[Persoon*Naam]
            ,   "BIG-nummer"    : (inschrijving[RegisterId*InschrijfId];bignummer[InschrijfId*Bignummer]) cRud
            ,   "Registratie"   :  inschrijving[RegisterId*InschrijfId];registratie [Registratie*InschrijfId]~ LINKTO INTERFACE Registratie_2

            ]

    INTERFACE Registratie_2 FOR MEDEWERKER : I[Registratie] cRud
        BOX <FORM  hideOnNoRecords  title="Beroepregister2"  showNavMenu> 
            [   "Register"      : registratie [Registratie*InschrijfId];inschrijving[RegisterId*InschrijfId]~ cRud
            ,   "Registratie"   : I[Registratie] cRud
            ,   "Inschrijving"  : registratie [Registratie*InschrijfId] LINKTO INTERFACE Inschrijving
            ,   "Deeltoegang"   : registratie [Registratie*InschrijfId];deelToegang [InschrijfId*Ja_of_Nee] cRud
            ,   "Persoon"       : registratie [Registratie*InschrijfId];inschrijving [Persoon*InschrijfId]~ LINKTO INTERFACE Persoon  
            ,   "BIG-nummer"    : registratie [Registratie*InschrijfId];bignummer [InschrijfId*Bignummer] cRud 
            ,   "Specialisme"   : specialist [Registratie * SpecialismeTandarts] cRUd
            ,   "Aantekening"   : aantekening [Registratie*Aantekening] CRUD
            ]

ENDCONTEXT
